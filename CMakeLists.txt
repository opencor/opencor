PROJECT(OpenCOR)

# Minimum version of CMake required

CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

# Type of build

SET(CMAKE_BUILD_TYPE Release)

# Version of OpenCOR

SET(${PROJECT_NAME}_VERSION_MAJOR 0 CACHE STRING "${PROJECT_NAME} major version number")
SET(${PROJECT_NAME}_VERSION_MINOR 1 CACHE STRING "${PROJECT_NAME} minor version number")
SET(${PROJECT_NAME}_VERSION_PATCH 1 CACHE STRING "${PROJECT_NAME} build version number")

SET(${PROJECT_NAME}_VERSION "${${PROJECT_NAME}_VERSION_MAJOR}.${${PROJECT_NAME}_VERSION_MINOR}.${${PROJECT_NAME}_VERSION_PATCH}")

# Files that make up OpenCOR

SET(${PROJECT_NAME}_SOURCES
   src/main.cpp
   src/mainwindow.cpp
)

SET(${PROJECT_NAME}_HEADERS
)

SET(${PROJECT_NAME}_HEADERS_MOC
   src/mainwindow.h
)

SET(${PROJECT_NAME}_UIS
   src/mainwindow.ui
)

SET(${PROJECT_NAME}_RESOURCES
)

# Set the application icon for Mac OS X

IF(APPLE)
   SET(MACOSX_BUNDLE_ICON_FILE ${PROJECT_NAME}.icns)
   SET_SOURCE_FILES_PROPERTIES(res/${PROJECT_NAME}.icns PROPERTIES MACOSX_PACKAGE_LOCATION Resources)
   SET(${PROJECT_NAME}_SOURCES ${${PROJECT_NAME}_SOURCES} res/${PROJECT_NAME}.icns)
ENDIF()

# Qt specific

FIND_PACKAGE(Qt4 4.7 REQUIRED)

IF(WIN32)
   SET(QT_USE_QTMAIN TRUE)
   # Note: the above ensures that winmain@16 is available on Windows
ENDIF()

INCLUDE(${QT_USE_FILE})

QT4_WRAP_CPP(${PROJECT_NAME}_SOURCES_MOC ${${PROJECT_NAME}_HEADERS_MOC})
QT4_WRAP_UI(${PROJECT_NAME}_SOURCES_UIS ${${PROJECT_NAME}_UIS})
QT4_ADD_RESOURCES(${PROJECT_NAME}_SOURCES_RCS ${${PROJECT_NAME}_RESOURCES})

# Rules to build OpenCOR itself

SET(CMAKE_INCLUDE_CURRENT_DIR ON)

ADD_EXECUTABLE(${PROJECT_NAME} WIN32 MACOSX_BUNDLE
   ${${PROJECT_NAME}_SOURCES}
   ${${PROJECT_NAME}_SOURCES_MOC}
   ${${PROJECT_NAME}_SOURCES_UIS}
   ${${PROJECT_NAME}_SOURCES_RCS}
)

TARGET_LINK_LIBRARIES(${PROJECT_NAME}
   ${QT_LIBRARIES}
)

# Packaging rules

INCLUDE(InstallRequiredSystemLibraries)

SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${PROJECT_NAME}, a cross-platform CellML-based modelling environment")
SET(CPACK_PACKAGE_VENDOR "Alan Garny")
SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README.txt")
SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LicenseLGPLv2.1.txt")
SET(CPACK_PACKAGE_VERSION_MAJOR "${${PROJECT_NAME}_VERSION_MAJOR}")
SET(CPACK_PACKAGE_VERSION_MINOR "${${PROJECT_NAME}_VERSION_MINOR}")
SET(CPACK_PACKAGE_VERSION_PATCH "${${PROJECT_NAME}_VERSION_PATCH}")
SET(CPACK_PACKAGE_INSTALL_DIRECTORY "${PROJECT_NAME}")
SET(CPACK_PACKAGE_EXECUTABLES "${PROJECT_NAME}" "${PROJECT_NAME}")

IF(WIN32)
   SET(CPACK_BINARY_NSIS ON)
ELSEIF(APPLE)
   SET(CPACK_BINARY_DRAGNDROP ON)
ELSE()
   SET(CPACK_BINARY_STGZ ON)
ENDIF()

INSTALL(TARGETS ${PROJECT_NAME} DESTINATION bin)

IF(WIN32)
   # MinGW files required by OpenCOR on Windows

   INSTALL(FILES ${QT_BINARY_DIR}/../../mingw/bin/mingwm10.dll DESTINATION bin)
   INSTALL(FILES ${QT_BINARY_DIR}/../../mingw/bin/libgcc_s_dw2-1.dll DESTINATION bin)

   # Qt files required by OpenCOR on Windows

   INSTALL(FILES ${QT_BINARY_DIR}/QtCore4.dll DESTINATION bin)
   INSTALL(FILES ${QT_BINARY_DIR}/QtGui4.dll DESTINATION bin)
ENDIF()

INCLUDE(CPack)
